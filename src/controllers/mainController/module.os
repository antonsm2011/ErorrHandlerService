#Использовать json
#Использовать tempfiles

&HttpMethod("POST")
Функция getInfo() Экспорт

	Возврат Содержимое(
		"{
		|	""needSendReport"": true,
		|   ""userMessage"": ""42""
		|}", 
		"application/json"
	);

КонецФункции

&HttpMethod("POST")
Процедура pushReport() Экспорт

	ВременныйКаталог   = ВременныеФайлы.СоздатьКаталог();
	ВременныйФайлАрхив = ВременныеФайлы.СоздатьФайл("zip");
	
	ПотокТелаЗапроса = ЗапросHttp.ДанныеФормы.Файлы[0].ОткрытьПотокДляЧтения();
	ЧтениеДанных = Новый ЧтениеДанных(ПотокТелаЗапроса);
	ДДЗапроса = ЧтениеДанных.Прочитать().ПолучитьДвоичныеДанные();
	ДДЗапроса.Записать(ВременныйФайлАрхив);
	ЧтениеДанных.Закрыть();

	ЧтениеZipФайла = Новый ЧтениеZipФайла(ВременныйФайлАрхив);
	ЧтениеZipФайла.ИзвлечьВсе(ВременныйКаталог);
	ЧтениеZipФайла.Закрыть();

	ЧтениеТекста = Новый ЧтениеТекста(ОбъединитьПути(ВременныйКаталог, "report.json"));
	ОтчетОбОшибкеВФорматеJSON = ЧтениеТекста.Прочитать();
	ЧтениеТекста.Закрыть();

	ПарсерJSON = Новый ПарсерJSON;
	ДанныеОтчетаОбОшибке = ПарсерJSON.ПрочитатьJSON(ОтчетОбОшибкеВФорматеJSON,,, Истина);

	Скриншот = Неопределено;
	ДополнительныеФайлы = Новый Массив();

	Если ДанныеОтчетаОбОшибке.Свойство("screenshot") Тогда
	
		Файл = Новый Файл(ОбъединитьПути(ВременныйКаталог, ДанныеОтчетаОбОшибке.screenshot.file));
		
		Если Файл.Существует() Тогда
			
			Скриншот = ПолучитьBase64СтрокуИзДвоичныхДанных(
				Новый ДвоичныеДанные(Файл.ПолноеИмя)
			);

		КонецЕсли;

	КонецЕсли;

	Если ДанныеОтчетаОбОшибке.Свойство("additionalFiles") Тогда

		Для Каждого ИмяФайла Из  ДанныеОтчетаОбОшибке.additionalFiles Цикл

			Файл = Новый Файл(ОбъединитьПути(ВременныйКаталог, ИмяФайла));
		
			Если Файл.Существует() Тогда
				ДополнительныеФайлы.Добавить(
					ПолучитьBase64СтрокуИзДвоичныхДанных(
						Новый ДвоичныеДанные(Файл.ПолноеИмя)
					)
				);
			КонецЕсли;		

		КонецЦикла;

	КонецЕсли;

	ВременныеФайлы.Удалить();

	МенеджерБазы.ДобавитьОтчетОбОшибке(ОтчетОбОшибкеВФорматеJSON, ДанныеОтчетаОбОшибке, Скриншот, ДополнительныеФайлы);

КонецПроцедуры

&HttpMethod("GET")
Функция getExceptions() Экспорт

	Запись = Новый ПарсерJSON;

	Возврат Содержимое(
		Запись.ЗаписатьJSON(МенеджерБазы.ПолучитьСписокОтчетовОбОшибке())	
	);

КонецФункции
